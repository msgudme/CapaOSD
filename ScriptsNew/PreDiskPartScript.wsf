<job>
	<script language="VBScript">
' // ***** Script Header *****
' //
' // File:      PreDiskPartScript.wsf
' //
' // *************************
'Global script Declarations

Dim gsScriptVersion:gsScriptVersion="13:55 5. september 2017"
Dim gsScriptName : gsScriptName="PreDiskPartScript.Wsf"

'*************************************************************************************
'*  Library Declarations
'*************************************************************************************
Dim bStatus
Dim giMessageBoxMode : giMessageBoxMode=0  '0=No Message ,1=Message, 2=Popup
Dim giMessageBoxTimeOut : giMessageBoxTimeOut=10  'Timeout secunds
Dim gbUUID : gbUUID=True


'*************************************************************************************

	Private Function IncludeScript(sScriptFile)
		Dim oLocalFso
		Dim sCmd
		Dim sClientDir
		Dim oLocalShell
		'Begin
		bStatus=True
		On Error Resume Next
		Set oLocalShell=CreateObject("WScript.Shell")
		Set oLocalFSO = CreateObject("Scripting.FileSystemObject")
		sClientDir=oLocalShell.RegRead("HKLM\Software\CapaSystems\CapaInstaller\Client Directory")
		sScriptDir = Mid(WScript.ScriptFullName,1,Len(WScript.ScriptFullName)-Len(WScript.ScriptName)-1)
		If bStatus and (sClientDir<>"") Then
			If oLocalFSO.FileExists(sScriptDir & "\" & sScriptFile) Then
				sCmd = oLocalFSO.OpenTextFile(sScriptDir & "\" & sScriptFile,1).ReadAll
			ElseIf oLocalFSO.FileExists(sClientDir & "\Lib\" & sScriptFile) Then
				sCmd = oLocalFSO.OpenTextFile(sClientDir & "\Lib\" & sScriptFile,1).ReadAll
			End If
		Else
			bStatus=False
		End If
		ExecuteGlobal sCmd
		IncludeScript=bStatus
	End Function

Function Custom()
	Dim bStatus
	'Begin
	bStatus=True
	If bStatus Then Call Custom_Check_ModelName()
    sStaticGroupName="Building"

	If bStatus Then bStatus=CMS_SubmitWinPEInventory()
    If bStatus Then bStatus=Custom_DriverGetModelName()
    If bStatus Then sDriverModelName=gsValue
    If ucase(sDriverModelName)=ucase("Unknown Model") Then
		Call Job_ShowMessage("PreDiskPartScript-Custom", "This computer model is not recognized by CAPA. The installation will probably fail." + _
							vbCrlf + vbCrlf + "Please contact the person most likely to be able to correct the matter." + _
							vbCrlf + vbCrlf + "gsModelName: " + gsModelName , 0,48,86400)
	End If

	If bStatus Then Call Custom_RemoveComputerFromStaticGroup(gsUUID,"Finished")
    If bStatus Then bStatus=Custom_AddUnittoGroup(gsUUID,"OSD Model - " & gsImageOSType & " - " & sDriverModelName)
    If bStatus Then bStatus=Custom_AddUnittoGroup(gsUUID,"OSD Model Name - " & gsImageOSType & " - " & gsModelName)
    If bStatus Then bStatus=Custom_AddUnittoGroup(gsUUID,"OSD Type - " & gsImageOSType & " - " & gsPCType)
	If bStatus Then bStatus=Custom_AddUnittoGroup(gsUUID,sStaticGroupName)
	Custom=bStatus
End Function



'*************************************************************************************
'*  Library Functions
'*************************************************************************************
Function Custom_OSD_Initialize()
	Dim bStatus, sFunction
	bStatus = True
	sFunction = "Custom_OSD_Initialize"

    'Read Inventar data from registry - updated from preguiscript
    If bStatus Then bStatus=Reg_GetString("HKLM","Software\CapaSystems\tst\Inventar","barcode",False):gsInventar_BarCode=gsValue
    If bStatus Then bStatus=Reg_GetString("HKLM","Software\CapaSystems\tst\Inventar","location",False):gsInventar_Location=gsValue
    If bStatus Then bStatus=Reg_GetString("HKLM","Software\CapaSystems\tst\Inventar","location_person",False):gsInventar_LocationPerson=gsValue
    If bStatus Then bStatus=Reg_GetString("HKLM","Software\CapaSystems\tst\Inventar","mac_address",False):gsInventar_MacAddress=gsValue
    If bStatus Then bStatus=Reg_GetString("HKLM","Software\CapaSystems\tst\Inventar","serial_number",False):gsInventar_SerialNumber=gsValue
    If bStatus Then bStatus=Reg_GetString("HKLM","Software\CapaSystems\tst\Inventar","last_control",False):gsInventar_LastControl=gsValue

	If CUSTOM_DEBUG then Call Msgbox("gsImageName = " & """" & gsImageName & """" & vbCrlf & _
									"gsModelName = " & """" & gsModelName & """" & vbCrlf & _
									"gsMachineType = " & """" & gsMachineType & """" & vbCrlf & _
									"gsNewComputerName = " & """" & gsNewComputerName & """")
	Custom_OSD_Initialize=bStatus
End Function


'*************************************************************************************

'Begin main
	bStatus=True
	If bStatus Then bStatus=IncludeScript("customlib.cis")
	If bStatus Then bStatus=IncludeLibrary("capalib.cin") 'legacy boot - OSDGUI
	If bStatus Then bStatus=IncludeLibrary("osdlib.cin") 'legacy boot - OSDGUI
	If bStatus Then bStatus=IncludeScript("OSDCustomlib.cis")

	If bStatus Then bStatus=Job_Start("WS","PreDiskPart Script","v1.0","PreDiskPartScript.Log","INSTALL")
	If bStatus Then bStatus=OSD_Initialize()
	If bStatus Then bStatus=Reg_SetString("HKLM","Software\CapaSystems\CapaInstaller","PackageRoot","s:") 'fix library legacy boot
	If bStatus Then bStatus=Custom_OSD_Initialize()
	If bStatus Then bStatus=Custom()
	bStatus=Job_End(bStatus)
	'End Main
	</script>
</job>
